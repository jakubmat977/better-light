*****************************************************************
* INFINEON TECHNOLOGIES                                         *
* BCR320U BCR321U BCR420U BCR421 SPICE2G6 MODEL                 *
* Version: 1.0                                                  *
* Attention! Preliminary version, will be updated soon!         * 
* Date: 2012-03-02                                              *
* Author: J.P. Schaffer                                         *
*****************************************************************
* SPICE2G6 MODEL OF THE PNP BIPOLAR TRANSISTOR IN BCR42xU       *
*****************************************************************
.MODEL T0114K NPN (IS=102F NF=1.0 BF=2.704K VAF=87.63 IKF=0.950
+ ISE=1.429F NE=1.110 NR=1.030 BR=17.67 VAR=1.380 IKR=0.663
+ ISC=3.85F NC=1.2
+ RB=0.312 IRB=0.100M RBM=0.228
+ RE=0.130 RC=0.403
+ CJE=44.900P VJE=0.662 MJE=0.33 FC=0.500
+ CJC=9.00P VJC=0.750 MJC=0.44 XCJC=1
+ TF=0.810N TR=2.600N PTF=1.000
+ XTF=4.300 VTF=1.500 ITF=0.450
+ XTB=1.500 EG=1.110 XTI=4.800
+ KF=5.000F AF=1.000)
*****************************************************************
* SPICE2G6 MODEL OF INTERNAL DIODE IN BCR42xU                   *
*****************************************************************
.MODEL D0306A D(
+ IS=1.5E-16 N=1.072 RS=3.2
+ IBV=1.5E-07 BV=2.0
+ M=0.51 CJO=1E-14 EG=1.11E+00
+ TT=8.66E-09 VJ=2.00E+00 XTI=5.0E+00)
*****************************************************************
* Circuit: BCR320U                                              *
* Nodes 1, 2, 3, 4, 5, 6 correspond to repective pin numbers    *
*****************************************************************
.subckt BCR320U 1 2 3 4 5 6
RINT  4  6     95    TC1=1.45E-3 TC2=5.6E-6
RB   10  1     10.0k TC1=47.0E-6 TC2=2.2E-6
D1   30  4     D0306A
D2   10 30     D0306A
Q1   20 10  6  T0114K AREA=1.2
R1   20  2     0.01
R2   20  3     0.01
R3   20  5     0.01
.ends BCR320U
*****************************************************************
* Circuit: BCR321U                                              *
* Nodes 1, 2, 3, 4, 5, 6 correspond to repective pin numbers    *
*****************************************************************
.subckt BCR321U 1 2 3 4 5 6
RINT  4  6     95    TC1=1.45E-3 TC2=5.6E-6
RB   10  1     1.5k  TC1=50E-6 TC2=1.3E-6
D1   30  4     D0306A
D2   10 30     D0306A
Q1   20 10  6  T0114K AREA=1.2
R1   20  2     0.01
R2   20  3     0.01
R3   20  5     0.01
.ends BCR321U
*****************************************************************
* Circuit: BCR420U                                              *
* Nodes 1, 2, 3, 4, 5, 6 correspond to repective pin numbers    *
*****************************************************************
.subckt BCR420U 1 2 3 4 5 6
RINT  4  6     95  TC1=1.45E-3 TC2=5.6E-6
RB   10  1     20k TC1=-500E-6 TC2=3.5E-6
D1   30  4     D0306A
D2   10 30     D0306A
Q1   20 10  6  T0114K AREA=0.66
R1   20  2     0.01
R2   20  3     0.01
R3   20  5     0.01
.ends BCR420U
*****************************************************************
* Circuit: BCR421U                                              *
* Nodes 1, 2, 3, 4, 5, 6 correspond to repective pin numbers    *
*****************************************************************
.subckt BCR421U 1 2 3 4 5 6
RINT  4  6     95	TC1=1.45E-3 TC2=5.6E-6
RB   10  1     1.5k	TC1=50E-6 TC2=1.3E-6
D1   30  4     D0306A
D2   10 30     D0306A
Q1   20 10  6  T0114K AREA=0.66
R1   20  2     0.01
R2   20  3     0.01
R3   20  5     0.01
.ends BCR421U